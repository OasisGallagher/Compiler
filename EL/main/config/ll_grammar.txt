$program : $stmt_seq

$stmt_seq : $stmt_seq ; $stmt | $stmt

$stmt : $if_stmt | $repeat_stmt | $assign_stmt | $read_stmt | $write_stmt | epsilon

$if_stmt : if $exp then $stmt_seq end | if $exp then $stmt_seq else $stmt_seq end

$repeat_stmt : repeat $stmt_seq until $exp

$assign_stmt : identifier = $exp

$read_stmt : read identifier

$write_stmt : write identifier

$exp : $simple_exp < $simple_exp | $simple_exp <= $simple_exp | $simple_exp == $simple_exp | $simple_exp >= $simple_exp | $simple_exp > $simple_exp | $simple_exp

$simple_exp : $simple_exp + $term | $simple_exp - $term | $term

$term : $term * $factor | $term / $factor | $factor

$factor : ( $exp ) | number | identifier
